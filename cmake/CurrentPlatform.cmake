list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/cmake/modules)

include(cmake/DependenciesLinux.cmake)
include(cmake/DependenciesDarwin.cmake)
include(cmake/DependenciesPSP.cmake)
include(cmake/DependenciesWindows.cmake)
include(cmake/DependenciesAndroid.cmake)

macro(spelunky_psp_detect_platform)

    if (DEFINED PLATFORM_PSP)
        set(SPELUNKY_PSP_PLATFORM "PSP")
        set(SPELUNKY_PSP_PLATFORM_PSP TRUE)
    endif()
    
    if (${CMAKE_SYSTEM_NAME} STREQUAL Android)
        set(SPELUNKY_PSP_PLATFORM "Android")
        set(SPELUNKY_PSP_PLATFORM_ANDROID TRUE)
    endif()

    if (DEFINED SPELUNKY_PSP_PLATFORM)
        # Nothing to do - SPELUNKY_PSP_PLATFORM was already defined in the toolchain file.
    elseif (${CMAKE_SYSTEM_NAME} STREQUAL "Linux")
        set(SPELUNKY_PSP_PLATFORM "Linux")
        set(SPELUNKY_PSP_PLATFORM_LINUX TRUE)
        set(SPELUNKY_PSP_PLATFORM_DESKTOP TRUE)
    elseif (${CMAKE_SYSTEM_NAME} STREQUAL "Darwin")
        set(SPELUNKY_PSP_PLATFORM "Darwin")
        set(SPELUNKY_PSP_PLATFORM_DARWIN TRUE)
        set(SPELUNKY_PSP_PLATFORM_DESKTOP TRUE)
    elseif (${CMAKE_SYSTEM_NAME} STREQUAL "Windows")
        set(SPELUNKY_PSP_PLATFORM "Windows")
        set(SPELUNKY_PSP_PLATFORM_WINDOWS TRUE)
        set(SPELUNKY_PSP_PLATFORM_DESKTOP TRUE)
    else ()
        message(FATAL_ERROR "Your platform (${CMAKE_SYSTEM_NAME}) is not supported")
    endif ()

    message(STATUS "Target platform is: ${SPELUNKY_PSP_PLATFORM}")
endmacro()

macro(spelunky_psp_add_platform_dependencies)
    if (${SPELUNKY_PSP_PLATFORM} STREQUAL PSP)
        add_psp_dependencies()
    elseif (${SPELUNKY_PSP_PLATFORM} STREQUAL Linux)
        add_linux_dependencies()
    elseif (${SPELUNKY_PSP_PLATFORM} STREQUAL Darwin)
        add_darwin_dependencies()
    elseif (${SPELUNKY_PSP_PLATFORM} STREQUAL Windows)
        add_windows_dependencies()
    elseif (${SPELUNKY_PSP_PLATFORM} STREQUAL Android)
        add_android_dependencies()
    endif ()
endmacro()

macro(spelunky_psp_post_build)
    if (${SPELUNKY_PSP_PLATFORM} STREQUAL PSP)
        spelunky_psp_post_build_psp()
    elseif (${SPELUNKY_PSP_PLATFORM} STREQUAL Linux)
        spelunky_psp_post_build_linux()
    elseif (${SPELUNKY_PSP_PLATFORM} STREQUAL Darwin)
        spelunky_psp_post_build_darwin()
    elseif (${SPELUNKY_PSP_PLATFORM} STREQUAL Windows)
        spelunky_psp_post_build_windows()
    elseif (${SPELUNKY_PSP_PLATFORM} STREQUAL Android)
        spelunky_psp_post_build_android()
    endif ()
endmacro()
